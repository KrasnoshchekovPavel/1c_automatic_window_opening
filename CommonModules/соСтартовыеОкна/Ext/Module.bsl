#Область СлужебныйПрограммныйИнтерфейс

Функция ЗагрузитьНастройки() Экспорт
	
	Настройки = ХранилищеОбщихНастроек.Загрузить("со/Настройки");
	
	Если Настройки = Неопределено Тогда
		Настройки = Новый Структура;
		Настройки.Вставить("ВариантАвтоматическогоОткрытия", "Отключить");
		Настройки.Вставить("ОткрываемыеНавигационныеСсылки", Новый Массив);
		Настройки.Вставить("ИнтервалСохраненияОткрытыхОкон", 10);
		СохранитьНастройки(Настройки);
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Функция ВажноеИзбранное() Экспорт
	
	Избранные = ХранилищеСистемныхНастроек.Загрузить("Общее/ИзбранноеРаботыПользователя");
	
	ВажноеИзбранное = Новый Массив;
	
	Если ТипЗнч(Избранные) <> Тип("ИзбранноеРаботыПользователя") Тогда
		Возврат ВажноеИзбранное;
	КонецЕсли;
	
	Для каждого Избранное Из Избранные Цикл
		Если Избранное.Важное Тогда
			ВажноеИзбранное.Добавить(Избранное.НавигационнаяСсылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВажноеИзбранное;
	
КонецФункции

Процедура СохранитьНастройки(Настройки) Экспорт
	
	ХранилищеОбщихНастроек.Сохранить("со/Настройки",, Настройки);	
	
КонецПроцедуры

Процедура СохранитьОткрываемыеНавигационныеСсылки(НавигационныеСсылки) Экспорт
	
	Настройки = ЗагрузитьНастройки();
	Настройки.ОткрываемыеНавигационныеСсылки = НавигационныеСсылки;
	СохранитьНастройки(Настройки);
	
КонецПроцедуры

#КонецОбласти
