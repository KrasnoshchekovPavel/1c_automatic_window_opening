
&После("ПриНачалеРаботыСистемы")
Процедура соПриНачалеРаботыСистемы()
	
	Если СтрНайти(ПараметрЗапуска, "ОтключитьСтартовыеОкна") > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = соСтартовыеОкнаВызовСервера.ЗагрузитьНастройкиДляАвтоматическогоОткрытия();
	НастройкиПодсистемы = Настройки.НастройкиПодсистемы;
	ВажноеИзбранное = Настройки.ВажноеИзбранное;
	
	Если НастройкиПодсистемы.ВариантАвтоматическогоОткрытия = "Отключить" Тогда
		
		Возврат;
		
	ИначеЕсли НастройкиПодсистемы.ВариантАвтоматическогоОткрытия = "ВажныеИзбранные" Тогда
		
		Для каждого Ссылка Из ВажноеИзбранное Цикл
			соСтартовыеОкнаКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке(Ссылка);
		КонецЦикла;
		
	ИначеЕсли НастройкиПодсистемы.ВариантАвтоматическогоОткрытия = "ФиксированныйСписок"
		Или НастройкиПодсистемы.ВариантАвтоматическогоОткрытия = "ОткрытыеОкна" Тогда
		
		Для каждого Ссылка Из НастройкиПодсистемы.ОткрываемыеНавигационныеСсылки Цикл
			соСтартовыеОкнаКлиент.ПопытатьсяПерейтиПоНавигационнойСсылке(Ссылка);
		КонецЦикла;
		
	КонецЕсли;
	
	Если НастройкиПодсистемы.ВариантАвтоматическогоОткрытия = "ОткрытыеОкна" Тогда
		соСтартовыеОкнаКлиент.ПодключитьОбработчикОжиданияСохраненияОткрытыхОкон();
	КонецЕсли;
	
КонецПроцедуры

Процедура соСохранитьОткрываемыеНавигационныеСсылки() Экспорт
	
	Окна = ПолучитьОкна();
	
	НавигационныеСсылки = Новый Соответствие;
	
	Для каждого Окно Из Окна Цикл
		НавигационныеСсылки.Вставить(Окно.ПолучитьНавигационнуюСсылку());
	КонецЦикла;
	
	НавигационныеСсылкиМассив = Новый Массив;
	
	Для каждого КЗ Из НавигационныеСсылки Цикл
		НавигационныеСсылкиМассив.Добавить(КЗ.Ключ);
	КонецЦикла;
	
	соСтартовыеОкнаВызовСервера.СохранитьОткрываемыеНавигационныеСсылки(НавигационныеСсылкиМассив);
	
КонецПроцедуры

